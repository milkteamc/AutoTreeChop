plugins {
    id 'java-library'
    id 'com.gradleup.shadow'
    id 'xyz.jpenilla.run-paper'
}

dependencies {
    // Depend on the core module
    implementation project(':core')

    // Spigot-specific dependencies
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    compileOnly "org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT"
    implementation "com.jeff_media:SpigotUpdateChecker:3.0.4"
    compileOnly 'me.clip:placeholderapi:2.11.5'
    implementation "de.cubbossa:TinyTranslations-bukkit:4.5.0"
    compileOnly files("${rootProject.projectDir}/libs/Residence5.1.4.3.jar")
    compileOnly "com.github.angeschossen:LandsAPI:6.44.0"
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.9'
    compileOnly "com.github.GriefPrevention:GriefPrevention:16.18.4"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveBaseName.set('AutoTreeChop-Spigot')
    archiveClassifier.set('')
    configurations = [project.configurations.runtimeClasspath]
    minimize()

    relocate 'com.jeff_media.updatechecker', 'org.milkteamc.autotreechop.libs.updatechecker'
    relocate 'de.cubbossa.tinytranslations', 'org.milkteamc.autotreechop.libs.tinytranslations'
    relocate 'com.zaxxer.hikari', 'org.milkteamc.autotreechop.libs.hikari'

    // Add the license file to the JAR
    from("${rootProject.projectDir}/LICENSE") {
        into 'META-INF'
    }
}

jar {
    archiveBaseName.set('AutoTreeChop-Spigot')
    archiveClassifier.set('original')
}

tasks.build {
    dependsOn("shadowJar")
}

runPaper {
    minecraftVersion("1.21.4")
}

runPaper.folia.registerTask()