plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.0.0'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'com.modrinth.minotaur' version '2.+'
}

group = 'org.milkteamc'
version = '1.7.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = "https://nexus.leonardbausenwein.de/repository/maven-public/"
    }
    maven {
        url = "https://maven.playpro.com/"
    }
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = 'https://repo.xenondevs.xyz/releases'
    }
    maven {
        url = "https://repo.jeff-media.com/public/"
    }
    maven {
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    compileOnly "org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT"
    implementation "com.jeff_media:SpigotUpdateChecker:3.0.4"
    compileOnly 'me.clip:placeholderapi:2.11.6'
    implementation "de.cubbossa:TinyTranslations-bukkit:4.5.2"
    compileOnly files("./libs/Residence5.1.4.3.jar")
    compileOnly "com.github.angeschossen:LandsAPI:7.17.2"
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.9'
    compileOnly "com.github.GriefPrevention:GriefPrevention:16.18.4"
    implementation "com.zaxxer:HikariCP:6.3.0"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.build {
    dependsOn("shadowJar")
}


tasks.shadowJar {
    archiveFileName = "AutoTreeChop-${version}.jar"
    configurations = [project.configurations.runtimeClasspath]
    minimize()

    relocate 'com.jeff_media.updatechecker', 'org.milkteamc.autotreechop.libs.updatechecker'
    relocate 'de.cubbossa.tinytranslations', 'org.milkteamc.autotreechop.libs.tinytranslations'
    relocate 'com.zaxxer.hikari', 'org.milkteamc.autotreechop.libs.hikari'

    // Add the license file to the JAR
    from("LICENSE") {
        into 'META-INF'
    }
}

tasks.jar {
    archiveFileName = "AutoTreeChop-${version}-original.jar"
}

tasks.runServer {
    minecraftVersion("1.21.8")
}

def gitHash = 'unknown'
try {
    def proc = 'git rev-parse --short HEAD'.execute()
    proc.waitFor()
    if (proc.exitValue() == 0) {
        gitHash = proc.text.trim()
    }
} catch (Exception ignored) {}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "autotreechop"
    versionType = "alpha"

    versionNumber = version
    versionName = "${version}-${gitHash}"
    changelog = """
ðŸš¨ **This is a SNAPSHOT version, ONLY use it in test server!**  
Report any issues to our [GitHub](https://github.com/milkteamc/AutoTreeChop/issues)
""".stripIndent().trim()

    uploadFile = tasks.shadowJar.archiveFile

    gameVersions = [
            "1.17", "1.17.1",
            "1.18", "1.18.1", "1.18.2",
            "1.19", "1.19.1", "1.19.2", "1.19.3", "1.19.4",
            "1.20", "1.20.1", "1.20.2", "1.20.3", "1.20.4", "1.20.5", "1.20.6",
            "1.21", "1.21.1", "1.21.2", "1.21.3", "1.21.4", "1.21.5", "1.21.6", "1.21.7", "1.21.8"
    ]
    loaders = ["paper", "purpur", "folia", "spigot", "bukkit"]
}

runPaper.folia.registerTask()
