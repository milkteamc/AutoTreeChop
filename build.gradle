plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version "2.2.4"
}

group = 'org.milkteamc'
version = '1.5.1'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = "https://nexus.leonardbausenwein.de/repository/maven-public/"
    }
    maven {
        url = "https://maven.playpro.com/"
    }
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = 'https://repo.xenondevs.xyz/releases'
    }
    maven {
        url = "https://repo.jeff-media.com/public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT"
    implementation "com.jeff_media:SpigotUpdateChecker:3.0.3"
    compileOnly 'me.clip:placeholderapi:2.11.5'
    implementation "de.cubbossa:TinyTranslations-bukkit:4.5.0"
    compileOnly "net.coreprotect:coreprotect:22.2"
    compileOnly files("./libs/Residence5.1.4.3.jar")
    compileOnly "com.github.angeschossen:LandsAPI:6.44.0"
    implementation "com.zaxxer:HikariCP:4.0.3"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.build {
    dependsOn("shadowJar")
}


tasks.shadowJar {
    archiveFileName = "AutoTreeChop-${version}.jar"
    configurations = [project.configurations.runtimeClasspath]
    minimize()

    relocate 'com.jeff_media.updatechecker', 'org.milkteamc.autotreechop.libs.updatechecker'
    relocate 'de.cubbossa.tinytranslations', 'org.milkteamc.autotreechop.libs.tinytranslations'
    relocate 'com.zaxxer.hikari', 'org.milkteamc.autotreechop.libs.hikari'
}

tasks.jar {
    archiveFileName = "AutoTreeChop-${version}-original.jar"
}

tasks.runServer {
    minecraftVersion("1.21")
}

runPaper.folia.registerTask()
