plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'xyz.jpenilla.run-paper' version '3.0.2'
    id 'com.modrinth.minotaur' version '2.+'
}

group = 'org.milkteamc'
version = '1.7.2'

// Java Configuration
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
        name = 'placeholderapi'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }

    maven {
        name = 'xenondevs'
        url = 'https://repo.xenondevs.xyz/releases'
    }

    maven {
        name = 'enginehub'
        url = 'https://maven.enginehub.org/repo/'
    }

    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'

    implementation 'net.kyori:adventure-platform-bukkit:4.4.1'
    implementation 'net.kyori:adventure-text-minimessage:4.26.1'

    implementation 'com.github.Anon8281:UniversalScheduler:0.1.7'
    implementation 'com.zaxxer:HikariCP:7.0.2'
    implementation 'org.bstats:bstats-bukkit:3.1.0'

    compileOnly 'me.clip:placeholderapi:2.11.7'
    compileOnly files('./libs/Residence5.1.4.3.jar')
    compileOnly 'com.github.angeschossen:LandsAPI:7.17.2'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.15'
    compileOnly 'com.github.GriefPrevention:GriefPrevention:16.18.4'
}

def gitHash = 'unknown'
def gitBranch = 'unknown'
def gitMessage = 'No commit message available.'

try {
    def hashProc = 'git rev-parse --short=7 HEAD'.execute()
    hashProc.waitFor()
    if (hashProc.exitValue() == 0) {
        gitHash = hashProc.text.trim()
    }

    def branchProc = 'git rev-parse --abbrev-ref HEAD'.execute()
    branchProc.waitFor()
    if (branchProc.exitValue() == 0) {
        gitBranch = branchProc.text.trim()
    }

    def msgProc = 'git log -1 --pretty=%B'.execute()
    msgProc.waitFor()
    if (msgProc.exitValue() == 0) {
        gitMessage = msgProc.text.trim()
    }
} catch (Exception ignored) {
    // Git commands failed, use defaults
}

def isRelease = project.hasProperty('release')
def fullVersion = isRelease ? version : "${version}-alpha+${gitHash}"

processResources {
    def props = [version: fullVersion]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.build {
    dependsOn('shadowJar')
}

tasks.jar {
    archiveFileName = "AutoTreeChop-${fullVersion}-original.jar"
}

tasks.shadowJar {
    archiveFileName = "AutoTreeChop-${fullVersion}.jar"
    configurations = [project.configurations.runtimeClasspath]
    minimize()

    relocate 'com.jeff_media.updatechecker', 'org.milkteamc.autotreechop.libs.updatechecker'
    relocate 'net.kyori', 'org.milkteamc.autotreechop.libs.kyori'
    relocate 'com.zaxxer.hikari', 'org.milkteamc.autotreechop.libs.hikari'
    relocate 'org.bstats', 'org.milkteamc.autotreechop.libs.bstats'
    relocate 'com.github.Anon8281.universalScheduler', 'org.milkteamc.autotreechop.libs.universalScheduler'

    from('LICENSE') {
        into 'META-INF'
    }

    manifest {
        attributes(
            'Implementation-Version': fullVersion,
            'Git-Commit': gitHash,
            'Git-Branch': gitBranch
        )
    }
}

tasks.runServer {
    minecraftVersion('1.21.11')
}

runPaper.folia.registerTask()

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'autotreechop'
    versionType = 'alpha'

    versionNumber = fullVersion
    versionName = "v${fullVersion}"
    changelog = """
        ğŸš¨ **This is an ALPHA version - Use at your own risk!**
        
        **Branch:** `${gitBranch}`  
        **Commit:** `${gitHash}`
        
        **Latest Changes:**
        > ${gitMessage}
        
        ---
        
        ğŸ“ Report issues: [GitHub Issues](https://github.com/milkteamc/AutoTreeChop/issues)  
        ğŸ’¬ Get support: [Discord](https://discord.gg/uQ4UXANnP2)
    """.stripIndent().trim()

    uploadFile = tasks.shadowJar.archiveFile

    gameVersions = [
            '1.17', '1.17.1',
            '1.18', '1.18.1', '1.18.2',
            '1.19', '1.19.1', '1.19.2', '1.19.3', '1.19.4',
            '1.20', '1.20.1', '1.20.2', '1.20.3', '1.20.4', '1.20.5', '1.20.6',
            '1.21', '1.21.1', '1.21.2', '1.21.3', '1.21.4', '1.21.5', '1.21.6',
            '1.21.7', '1.21.8', '1.21.9', '1.21.10', '1.21.11'
    ]

    loaders = ['paper', 'purpur', 'folia', 'spigot']

    dependencies {
        optional.project 'placeholderapi'
    }
}